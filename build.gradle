defaultTasks 'dist'

//settings common to all projects
allprojects {
    apply plugin: 'java'
	
	compileJava.options.encoding = 'UTF-8'
	compileTestJava.options.encoding = 'UTF-8'
    
	sourceCompatibility = 6
	
	repositories {
		mavenCentral()
	}
	
	//additional configurations for compile classpaths
	configurations {
		compileOnly
		testCompileOnly { extendsFrom compileOnly }
	}
	
	dependencies {
		testCompile group: 'junit', name: 'junit', version: '4.+'
	}

	sourceSets {
		main {
			java { srcDir 'src' }
			resources { srcDir 'src' }
			compileClasspath += configurations.compileOnly
		}
		test {
			java { srcDir 'test' }
			resources { srcDir 'test' }
			compileClasspath += configurations.compileOnly + configurations.testCompileOnly
		}
	}

	jar {
		from 'license.txt'
		manifest.attributes 'Created-By': 'Xenoage Software'
	}
}

//project dependencies
project(':utils-base') { dependencies { compile project(':utils-kernel') } }
project(':utils-pdlib') { dependencies { compile project(':utils-base') } }
project(':utils-jse') { dependencies { compile project(':utils-base') } }
project(':utils-serialize') { dependencies { compile project(':utils-jse') } }
project(':utils-test') {
	sourceCompatibility = 8
	dependencies { compile project(':utils-jse'), project(':utils-pdlib'), project(':utils-serialize') }
}

//utils-gwt dependencies
project(':utils-gwt') { dependencies {
	compile project(':utils-base')
	compile group: 'com.google.gwt', name: 'gwt-user', version: '2.6.1'
} }

//distribution in multiple jars
task distDir(type:Copy) {
	dependsOn subprojects*.build
	from subprojects*.jar.outputs.files
	into "build/dist/split"
}

//distribution in single jar
task singleJar(type: Jar) {
	dependsOn subprojects*.build
	
	subprojects.each() { subproject ->
		ext.jarDir = "${subproject.name}/build/libs/"
		from (zipTree(ext.jarDir + subproject.name + ".jar")) { exclude 'license.txt' }
		from (zipTree(ext.jarDir + subproject.name + ".jar")) { include 'license.txt' into("licenses/${subproject.name}/") }
	};
	from 'license.txt'
	destinationDir = file("build/dist/onejar")
	archiveName = 'utils.jar'
	manifest.attributes 'Created-By': 'Xenoage Software'
}

//main task
task dist {
	dependsOn build
	dependsOn distDir, singleJar
	
	//javadocs - this is done in Ant, works much better than in Gradle
	ant.importBuild 'javadoc.build.xml'
	dependsOn antUtilsJavadoc
}